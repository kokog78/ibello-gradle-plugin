// building

apply plugin: "java"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
	compile gradleApi()
	testCompile group: 'org.assertj', name: 'assertj-core', version: '3.12.2'
	testCompile group: 'junit', name: 'junit', version: '4.12'
}

// signing

apply plugin: "signing"

signing {
    sign configurations.archives
}

// deploying

apply plugin: "maven"

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
artifacts {
    archives javadocJar, sourcesJar
}

group 'hu.ibello'
version '1.0'

// build, sign and upload

uploadArchives {
    repositories {
        mavenDeployer {

            // Sign POM
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            // Destination
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            // Add required metadata to POM
            pom.project {
                name 'Ibello Gradle Plugin'
                packaging 'jar'
                description 'Gradle plugin for the ibello test framework.'
                url 'https://github.com/kokog78/ibello-gradle-plugin'

                scm {
                    connection 'scm:git:git://github.com/kokog78/ibello-gradle-plugin.git'
                        developerConnection 'scm:git:ssh://github.com/kokog78/ibello-gradle-plugin.git'
                        url 'http://github.com/kokog78/ibello-gradle-plugin'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'kokog'
                        name 'KornÃ©l Simon'
                        email 'kornel.simon@ibello.hu'
                        organization 'ibello'
                        organizationUrl 'https://ibello.hu'
                    }
                }
            }
        }
    }
}

/*
Tested with gradle 6.4.1

Content of the gradle.properties should be:

signing.keyId={key id}
signing.password={key password}
signing.secretKeyRingFile={full path to secring.gpg}
ossrhUsername={ossrh user}
ossrhPassword={ossrh password}

The secring file can be generated with:
gpg --keyring secring.gpg --export-secret-keys > {full path to secring.gpg}

The key id can be obtained with:
gpg --list-keys --keyid-format 0xSHORT
*/
